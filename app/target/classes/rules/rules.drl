package rules;

import demo.model.*;

rule "Increase balance for AccountPeriod Credits"
when
   ap : AccountPeriod( )
   $acc : Account( $balance: balance ) @watch(!balance)
   cf : CashFlow( type == CashFlowType.CREDIT,
             accountNo == $acc.accountNo,
             date >= ap.start && <= ap.end, $amount: amount )
then
   modify($acc){setBalance($balance + $amount);}
end

rule "Decrease balance for AccountPeriod Debits"
when
   ap : AccountPeriod( )
   $acc : Account( $balance: balance ) @watch(!balance)
   cf : CashFlow( type == CashFlowType.DEBIT,
             accountNo == $acc.accountNo,
             date >= ap.start && <= ap.end, $amount: amount )
then
   modify($acc){setBalance($balance - $amount);}
end

rule "Print blance for AccountPeriod" 
salience -50
when
       ap : AccountPeriod()
       $acc : Account( )
then
       System.out.println( "Account Number " + $acc.getAccountNo() + " balance " + $acc.getBalance() );
end


